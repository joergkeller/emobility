plugins {
    id 'java'
}

group = 'edu.jke.emobility'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':adapter')
    implementation project(':usecase')
    implementation project(':domain')

    implementation 'org.springframework.boot:spring-boot-starter:3.4.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

// copies all the jar dependencies your app needs to the lib folder
task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/lib"
}

// creates your manifest file within the JAR
jar {
    manifest {
        attributes["Main-Class"] = "edu.jke.emobility.boot.SpringConsoleApplication"
        attributes["Class-Path"] = configurations.runtimeClasspath.collect { 'lib/' + it.getName() }.join(' ')
    }
}

task listDependencies {
    doLast {
        println("[");
        configurations.each { config ->
                if (config.canBeResolved) {
                    config.incoming.getResolutionResult().getAllDependencies().each { depResult ->
                        println "{\"config\":\"" + config.getName() + "\",\"from\":\"" + depResult.getFrom() + "\",\"requested\":\"" + depResult.getRequested() + "\"}"
                }
            }
        }
        println("]");
    }
}